#version 450 core
layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;
uniform int uVoxelFragmentNum, uVoxelResolution, uLevel;

layout(std430, binding = 2) readonly buffer uuFragmentList { uvec2 uFragmentList[]; };
layout(std430, binding = 3) buffer uuOctree { uint uOctree[]; };

void main()
{
	if(gl_GlobalInvocationID.x >= uVoxelFragmentNum) return;
	uvec2 ufragment = uFragmentList[gl_GlobalInvocationID.x];

	uint level_dim = uVoxelResolution;
	uvec3 level_pos = uvec3(ufragment.x & 0xfffu, (ufragment.x >> 12u) & 0xfffu, (ufragment.x >> 24u) | ((ufragment.y >> 28u) << 8u));
	bvec3 level_cmp;

	uint idx = 0, cur = 0;
	for(int i = 0; i < uLevel; ++i)
	{
		level_dim >>= 1;

		level_cmp = greaterThanEqual(level_pos, uvec3(level_dim));
		idx = cur + (uint(level_cmp.x) | (uint(level_cmp.y) << 1u) | (uint(level_cmp.z) << 2u));
		cur = uOctree[idx] & 0x7fffffffu;
		level_pos -= uvec3(level_cmp) * level_dim;
	}

	uOctree[idx] |= 0x80000000u;
	if((1u << uLevel) == uVoxelResolution) 
		uOctree[idx] = 0xC0000000u | (ufragment.y & 0xffffffu); //termination tag
}
